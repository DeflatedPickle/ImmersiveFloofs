buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
    mavenCentral()
    ivy {
        name 'asie dependency mirror'
        artifactPattern "http://asie.pl/javadeps/[module]-[revision](-[classifier]).[ext]"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

sourceCompatibility='1.8'
targetCompatibility='1.8'

configurations {
    deployerJars
    all {
        resolutionStrategy.cacheChangingModulesFor 1, 'seconds'
    }
}

ext {
    massive = "1"
    major = "0"
    minor = "0"
    mcVersion = "1.12.2"
    forgeVersion = "14.23.1.2587"
}

group= "uk.kihira.immersivefloofs"
archivesBaseName = "ImmersiveFloofs"
version = "${project.mcVersion}-${project.massive}.${project.major}.${project.minor}"

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
    deobfCompile name: "ImmersiveEngineering", version: "0.12-76"
    deobfCompile name: "Tails", version: "1.12-1.10.0"
}

minecraft {
    version = "${project.mcVersion}-${project.forgeVersion}"
    runDir = "run"
    mappings = "snapshot_20180111"

    replace '@VERSION@', project.version
    replace '@MCVERSION@', project.mcVersion
}

//noinspection GroovyAssignabilityCheck
processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

/**
 * Maven uploading
 */
def deployer = {
    repository(url: project.properties.maven_url) {
        configuration = configurations.deployerJars
        authentication(userName: project.properties.maven_username ?: "", password: project.properties.maven_password ?: "")
    }

    pom {
        groupId = project.group
        version = project.version
        artifactId = project.archivesBaseName

        project {
            name project.archivesBaseName
            packaging 'jar'
            description 'Immersive Floofs'
            url 'https://github.com/kihira/ImmersiveFloofs/'

            scm {
                url 'https://github.com/kihira/ImmersiveFloofs/'
                connection 'scm:git:git://github.com/kihira/ImmersiveFloofs.git'
                developerConnection 'scm:git:git@github.com/kihira/ImmersiveFloofs.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/kihira/ImmersiveFloofs/issues'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'Kihira'
                    name 'Kihira'
                    roles {
                        role 'developer'
                    }
                }
            }
        }
    }
}

uploadArchives {
    repositories.mavenDeployer(deployer)
}
